// plugin
plugins {
    id 'java'
    id 'application'
    id 'distribution'
    id 'org.springframework.boot' version '2.7.1'
    id 'io.spring.dependency-management' version '1.0.12.RELEASE'
    id 'net.nemerosa.versioning' version '3.0.0'
    id 'org.sonarqube' version '3.3'
}

// application
group = "com.witalis.praxis"
version = "1.0.0"
mainClassName = "com.witalis.praxis.leetcode.PraxisLeetcodeApplication"

// compatibility
sourceCompatibility = JavaVersion.VERSION_18
targetCompatibility = JavaVersion.VERSION_18

// extension
ext {
    title = "Praxis Leetcode"
    groupId = "com.witalis.praxis"
    moduleId = "leetcode"
    notification = "wellaxis@gmail.com"
    guide = "readme.md"
    // manifest
    manifestAttributes = [
        "Implementation-Title"   : project.title,
        "Implementation-Version" : project.version,
        "Created-By"             : "Gradle ${gradle.gradleVersion}",
        "Built-By"               : System.properties['user.name'],
        "Build-Timestamp"        : new Date().format("yyyy-MM-dd'T'HH:mm:ss.SSSZ").toString(),
        "Build-Revision"         : versioning.info.commit,
        "Build-Jdk"              : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
        "Build-OS"               : "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
    ]
}

// repository
repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url 'https://repo.spring.io/milestone'
    }
    maven {
        url 'https://plugins.gradle.org/m2/'
    }
}

// properties
springBoot {
    buildInfo()
}

// versioning
versioning {
    version = versioning.info.lastTag
    noWarningOnDirty = true
}

// compilation
tasks.withType(JavaCompile).configureEach {
    options.incremental = false
}

// configuration
configurations.all {
    transitive = true
    exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
}

// dependency
dependencies {
    // boot
    implementation "org.springframework.boot:spring-boot-starter"
    implementation "org.springframework.boot:spring-boot-starter-log4j2"
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
    // others
    implementation "commons-codec:commons-codec:1.+"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml"
    implementation "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:3.3"
    // lombok
    compileOnly "org.projectlombok:lombok:1.+"
    annotationProcessor "org.projectlombok:lombok:1.+"
    // test
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testCompileOnly "org.projectlombok:lombok:1.+"
    testAnnotationProcessor "org.projectlombok:lombok:1.+"
}

// information
task information {
    doFirst {
        println 'Leetcode application: BEGIN'
    }
    println "==================================================="
    println "=> Title:     " + project.title
    println "=> Version:   " + project.version
    println "=> Project:   " + project.rootDir
    println "=> Module:    " + rootProject.name
    println "=> Group:     " + project.groupId
    println "=> Guide:     " + project.guide
    println "=> E-mail:    " + project.notification
    println "==================================================="
    doLast {
        println 'Leetcode application: END'
    }
}
